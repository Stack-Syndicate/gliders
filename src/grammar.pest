program    =  { SOI ~ expression* ~ EOI }
expression =  { (definition | assignment) ~ ";" }

// Asignment and definitions
definition =  { "let" ~ identifier ~ "=" ~ (evaluation | literal) }
assignment =  { identifier ~ "=" ~ (evaluation | literal) }
identifier = @{ ASCII_ALPHA ~ ASCII_ALPHANUMERIC* }

// Literals
literal    =  { float | number | string | bool | list }
float      = @{ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }
number     = @{ ASCII_DIGIT+ }
string     = @{ "\"" ~ ("\\\"" | !("\"") ~ ANY)* ~ "\"" }
bool       =  { "true" | "false" }
list       =  { "[" ~ ((identifier|evaluation|literal) ~ ",")* ~ (identifier|evaluation|literal) ~ "]" }

// Operations and operators
evaluation  = { operation | term }
operation   = { term ~ (operator ~ term)+ }
term        = { literal | identifier | "(" ~ evaluation ~ ")" }
operator   =  { add | sub | mul | div }
add        = @{ "+" }
sub        = @{ "-" }
mul        = @{ "*" }
div        = @{ "/" }

// ---------------------------------------
WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT    = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
